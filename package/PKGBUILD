# Maintainer: Patrick Fischer <aur@pathin.me>

pkgbase=windsurf-bin
pkgname=(windsurf-bin{,-electron-latest})
pkgver=1.12.16
pkgrel=1
arch=('x86_64')
url="https://windsurf.com/"
license=('LicenseRef-Windsurf Editor')
depends=( ripgrep fd xdg-utils #replacements
    'alsa-lib'
    'dbus'
    'gnupg'
    'libnotify'
    'libsecret'
    'libxkbfile'
    'libxss'
)
optdepends=('glib2: Move to trash functionality'
            'org.freedesktop.secrets: Sync settings'
            'libdbusmenu-glib: KDE global menu'
            'lsof: Terminal splitting'
            'vulkan-driver')
options=('!strip')
makedepends=(tar sed desktop-file-utils)
source=("https://windsurf-stable.codeiumdata.com/wVxQEIWkwPUEAGf3/apt/pool/main/w/windsurf/Windsurf-linux-x64-${pkgver}.deb"
		"https://gitlab.archlinux.org/archlinux/packaging/packages/code/-/raw/main/code.sh")
sha256sums=('f40c1081dfb356ec6fdcb792f60219b59117388e8831aaa5c4521c38b159ff3b'
            '5da1525b5fe804b9192c05e1cbf8d751d852e3717fb2787c7ffe98fd5d93e8c1')
build() {
	tar -xf "data.tar.xz" --exclude 'usr/share/windsurf/[^r]*' --exclude 'usr/share/windsurf/*.pak'

	# Ensure metainfo exists and safely move contents from appdata if present
	mkdir -p usr/share/metainfo
	if [ -d usr/share/appdata ]; then
		shopt -s dotglob nullglob
		mv usr/share/appdata/* usr/share/metainfo/ || true
		shopt -u dotglob nullglob
		rmdir usr/share/appdata 2>/dev/null || true
	fi

	# Ensure zsh site-functions exists and safely move completions
	mkdir -p usr/share/zsh/site-functions
	if [ -d usr/share/zsh/vendor-completions ]; then
		shopt -s dotglob nullglob
		mv usr/share/zsh/vendor-completions/* usr/share/zsh/site-functions/ || true
		shopt -u dotglob nullglob
		rmdir usr/share/zsh/vendor-completions 2>/dev/null || true
	fi
	
	# Launcher
	_app=/usr/share/windsurf/resources/app
	sed -e "s|code-flags|windsurf-flags|" code.sh \
		-e "s|/usr/lib/code/out/cli.js|${_app}/out/cli.js|" \
		-e "s|/usr/lib/code/code.mjs|--app=${_app}|" > run.sh
	ln -sf /usr/bin/windsurf usr/share/windsurf/windsurf
	
	# Replacements
	# Ensure windsurd extension bin path exists for symlinks
	mkdir -p usr/share/windsurf/resources/app/extensions/windsurf/bin
	ln -svf /usr/bin/fd usr/share/windsurf/resources/app/extensions/windsurf/bin/fd
	ln -svf /usr/bin/rg usr/share/windsurf/resources/app/node_modules/@vscode/ripgrep/bin/rg
	ln -svf /usr/bin/xdg-open usr/share/windsurf/resources/app/node_modules/open/xdg-open
	
	# SVG Icon (handle path changes across versions)
	ICON_DEST="usr/share/icons/hicolor/scalable/apps/windsurf.svg"
	ICON_CANDIDATES=(
		"usr/share/windsurf/resources/app/out/media/code-icon.svg"
		"usr/share/windsurf/resources/app/out/media/code-iconsvg.svg"
		"usr/share/windsurf/resources/app/out/media/windsurf-icon.svg"
		"usr/share/windsurf/resources/app/out/vs/code/browser/workbench/media/code-icon.svg"
	)
	ICON_FOUND=""
	for icon in "${ICON_CANDIDATES[@]}"; do
		if [ -f "$icon" ]; then
			ICON_FOUND="$icon"
			break
		fi
	done
	if [ -n "$ICON_FOUND" ]; then
		install -Dm644 "$ICON_FOUND" "$ICON_DEST"
	else
		echo "Warning: SVG icon not found in known locations, skipping icon install." >&2
	fi
	
	# Hide entry of URL handler
	desktop-file-edit --set-key Hidden --set-value true usr/share/applications/windsurf-url-handler.desktop
}

package_windsurf-bin(){
	pkgdesc="Windsurf (formerly Codeium) is the world's most advanced AI coding assistant for developers and enterprises. Windsurf Editor â€” the first AI-native IDE that keeps developers in flow."
	cp -r --reflink=auto usr "${pkgdir}/usr"
	_electron=electron$(rg -o -r '$1' '"electron": *"[^0-9]*([0-9]+)' usr/share/windsurf/resources/app/package.json)
	echo $_electron
	sed "s|name=electron|name=${_electron}|" run.sh > run-safe.sh
	install -Dm755 run-safe.sh "${pkgdir}/usr/bin/windsurf"
	depends+=(${_electron}) # hidden from --printsrcinfo
	provides=(windsurf)
	conflicts=(windsurf windsurf-bin-electron-latest)
}

package_windsurf-bin-electron-latest(){
	pkgdesc="Windsurf Editor on latest stable electron"
	mv usr "${pkgdir}/usr" # breaks --repackage
	install -Dm755 run.sh "${pkgdir}/usr/bin/windsurf"
	depends+=(electron)
	conflicts=(windsurf-bin windsurf)
	provides=(windsurf-bin windsurf)
}